plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'application'
}

group = 'hotketok'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	runtimeOnly 'mysql:mysql-connector-java:8.0.33'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
}

tasks.named('test') {
	useJUnitPlatform()
}

// WebApp JAR 생성 태스크
task bootJarWebApp(type: org.springframework.boot.gradle.tasks.bundling.BootJar) {
	group = 'build'
	description = 'Build BootJar for WebAppApplication'
	archiveBaseName.set('webapp')
	mainClass.set('hotketok.hotketok_server.WebAppApplication')
}

// WebServer JAR 생성 태스크
task bootJarWebServer(type: org.springframework.boot.gradle.tasks.bundling.BootJar) {
	group = 'build'
	description = 'Build BootJar for WebServerApplication'
	archiveBaseName.set('webserver')
	mainClass.set('hotketok.hotketok_server.WebServerApplication')
}

// 기본 bootJar 태스크 비활성화
tasks.named('bootJar').configure {
	enabled = false
}

// WebApp 실행 태스크
task runWebApp(type: JavaExec) {
	group = 'application'
	description = 'Run WebApp server'
	mainClass = 'hotketok.hotketok_server.WebAppApplication'
	classpath = sourceSets.main.runtimeClasspath
}

// WebServer 실행 태스크
task runWeb(type: JavaExec) {
	group = 'application'
	description = 'Run Web server'
	mainClass = 'hotketok.hotketok_server.WebServerApplication'
	classpath = sourceSets.main.runtimeClasspath
}

application {
	mainClass = 'hotketok.hotketok_server.WebServerApplication'
}
